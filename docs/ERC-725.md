---
eip: 725
title: General data key/value store and execution 
description: An interface for a smart contract based account with attachable data key/value store
author: Fabian Vogelsteller (@frozeman), Tyler Yasaka (@tyleryasaka)
discussions-to: https://github.com/ethereum/EIPs/issues/725
status: Review
type: Standards Track
category: ERC
created: 2017-10-02
requires: 165, 173
---

## Abstract

The following describes two standards that allow for a generic data storage in a smart contract and a generic execution through a smart contract. These can be used seperate or in conjunction and can serve as building blocks for smart contract accounts, upgradable meta data and other means.

## Motivation

The initial motivation came out of the need to create a smart contract account system thats flexible enough to be viable long term, but also defined enough to be standardisable, but are in its current form a generic set of standardised modules to be used in all forms of smart contracts.

This standard consists of two sub standards, a generic data key/value store (ERC725Y) and a generic execute function (ERC725X). Both of which in combination allow for a very flexible and long lasting account system. The account verison of ERC725 is standardised under [LSP0 ERC725Account](https://github.com/lukso-network/LIPs/blob/master/LSPs/LSP-0-ERC725Account.md).

These standareds (ERC725 X and Y) can also be used seperately to enhance NFTs and Token meta data, or allow generic exection for all kinds of smart contracts.

## Specification

### Ownership

This contract is controlled by an owner. The owner can be a smart contract or an external account.
This standard requires [ERC173](https://github.com/ethereum/EIPs/blob/master/EIPS/eip-173.md) and SHOULD implement the functions:

- `owner() view`
- `transferOwnership(address newOwner)`

And the event:

- `OwnershipTransferred(address indexed previousOwner, address indexed newOwner)`

### ERC725X

ERC165 identifier: `0x44c028fe`

#### execute

```solidity
function execute(uint256 operationType, address to, uint256 value, bytes memory data) public payable returns(bytes memory)
```

Executes a call on any other smart contracts, transfers the blockchains native token, or deploys a new smart contract.
MUST only be called by the current owner of the contract.
MUST revert when the execution fails.

_Parameters:_

- `operationType`: the operation type used to execute.
- `to`: the smart contract or address to call. `to` will be unused if a contract is created (operation types 1 and 2).
- `value`: the amount of native tokens to transfer (in Wei).
- `data`: the call data, or the creation bytecode of the contract to deploy.

#### data parameter

- For operationType, `CALL`, `STATICCALL` and `DELEGATECALL` the data field can be random bytes or an abi-encoded function call.

- For operationType, `CREATE` the `data` field is the creation bytecode of the contract to deploy appended with the constructor argument(s) abi-encoded.

- For operationType, `CREATE2` the `data` field is the creation bytecode of the contract to deploy appended with:
    1. the constructor argument(s) abi-encoded
    2. a bytes32 salt. 

```
data = <contract-creation-code> + <abi-encoded-constructor-arguments> + <bytes32-salt>
```
    
> Check [EIP-1014: Skinny CREATE2](https://eips.ethereum.org/EIPS/eip-1014) for more information.
  

_Returns:_ `bytes` , the returned data of the called function, or the address of the contract deployed (operation types 1 and 2).

The following `operationType` MUST exist:

- `0` for `CALL`
- `1` for `CREATE`
- `2` for `CREATE2`
- `3` for `STATICCALL`
- `4` for `DELEGATECALL` - **NOTE** This is a potentially dangerous operation type

Others may be added in the future.

**Triggers Event:** [ContractCreated](#contractcreated), [Executed](#executed)

> **Note:** the operation types `staticcall` (`3`) and `delegatecall` (`4`) do not allow to transfer value.

### Events

#### Executed

```solidity
event Executed(uint256 indexed operationType, address indexed to, uint256 indexed _value, bytes4 data);
```

MUST be triggered when `execute` creates a new call using the `operationType` `0`, `3`, `4`.

#### ContractCreated

```solidity
event ContractCreated(uint256 indexed operationType, address indexed contractAddress, uint256 indexed value);
```

MUST be triggered when `execute` creates a new contract using the `operationType` `1`, `2`.

### ERC725Y

ERC165 identifier: `0x714df77c`

**Note:** `setData()` and `getData()` uses function overloading, therefore it is better to reference them by the given function signature as follows:

```js
myContract.methods['setData(bytes32[],bytes[])']([dataKeys, ...], [dataValues, ...]).send()
// or
myContract.methods['0x14a6e293']([dataKeys, ...], [dataValues, ...]).send()
```

#### getData

```solidity
function getData(bytes32 dataKey) public view returns(bytes memory)
```

Function Signature: `0x54f6127f`

Gets the data set for the given data key.

_Parameters:_

- `dataKey`: the key which value to retrieve.

_Returns:_ `bytes` , The data for the requested key.

#### getData (Array)

```solidity
function getData(bytes32[] memory dataKeys) public view returns(bytes[] memory)
```

Function Signature: `0x4e3e6e9c`

Gets array of data at multiple given key.

_Parameters:_

- `dataKeys`: the keys which values to retrieve.

_Returns:_ `bytes[]` , array of data values for the requested data keys.

#### setData

```solidity
function setData(bytes32 dataKey, bytes memory dataValue) public
```

Function Signature: `0x7f23690c`

Sets data as bytes in the storage for a single key. MUST only be called by the current owner of the contract.

_Parameters:_

- `dataKey`: the key which value to set.
- `dataValue`: the data to set.

**Triggers Event:** [DataChanged](#datachanged)

#### setData (Array)

```solidity
function setData(bytes32[] memory dataKeys, bytes[] memory dataValues) public
```

Function Signature: `0x14a6e293`

Sets array of data at multiple keys. MUST only be called by the current owner of the contract.

_Parameters:_

- `dataKeys`: the keys which values to set.
- `dataValues`: the array of bytes to set.

**Triggers Event:** [DataChanged](#datachanged)

### Events

#### DataChanged

```solidity
event DataChanged(bytes32 indexed dataKey, bytes dataValue)
```

MUST be triggered when a data key was successfully set.

### ERC725Y Data keys

Data keys, are the way to retrieve values via `getData()`. These `bytes32` values can be freely chosen, or defined by a standard.
A common ways to define data keys is the hash of a word, e.g. `keccak256('ERCXXXMyNewKeyType')` which results in: `0x6935a24ea384927f250ee0b954ed498cd9203fc5d2bf95c735e52e6ca675e047`

The [LSP2 ERC725JSONSchema standard](https://github.com/lukso-network/LIPs/blob/master/LSPs/LSP-2-ERC725YJSONSchema.md) is a more explicit ERC725Y data key standard, that defines key types and value types, and their encoding.

## Rationale

Special care went into ensuring that data in this smart contract can be read not only by external applications, but also by other smart contracts in a gas efficient manner. Therefore function overwriting is used to allow for both single and multiple data keys to be retreived.

The generic way of storing data key with values was chosen to allow upgradablity over time. Stored data values can be changed over time. Other smart contract protocols can then interpret this data in new ways and react to interactions from a ERC725 smart contract differently.

## Backwards Compatibility

All contracts since ERC725v2 from 2018/19 should be compatible to the current version of the standard. Mainly interface ID and Event parameters have changed, while `getData(bytes32[])` and `setData(bytes32[], bytes[])` was added as an efficient way to set/get multiple keys at once.

## Reference Implementation

Reference implementations can be found [here](../assets/eip-725)

## Security Consideration

This contract allows generic executions, therefore special care need to be take care to prevent re-entrancy attacks and other forms for call chain attacks.

When using the operation type `4` for `delegatecall`, it is important to consider that called contracts can alter the state of the calling contract and also change owner variables at will. Additionally calls to `selfdestruct` are possible and other harmful state changing operations.

### Solidity Interfaces

```solidity
// SPDX-License-Identifier: CC0-1.0
pragma solidity >=0.5.0 <0.7.0;

// ERC165 identifier: `0x44c028fe`
interface IERC725X  /* is ERC165, ERC173 */ {
    event ContractCreated(uint256 indexed operationType, address indexed contractAddress, uint256 indexed value);
    event Executed(uint256 indexed operationType, address indexed to, uint256 indexed  value, bytes4 data);

    function execute(uint256 operationType, address to, uint256 value, bytes memory data) external payable returns(bytes memory);
}

// ERC165 identifier: `0x714df77c`
interface IERC725Y /* is ERC165, ERC173 */ {
    event DataChanged(bytes32 indexed dataKey, bytes dataValue);

    function getData(bytes32 dataKey) external view returns(bytes memory);
    function getData(bytes32[] memory dataKeys) external view returns(bytes[] memory);

    function setData(bytes32 dataKey, bytes memory dataValue) external;
    function setData(bytes32[] memory dataKeys, bytes[] memory dataValues) external;
}

interface IERC725 /* is IERC725X, IERC725Y */ {

}

```

## Additional References

- [Slides of the ERC Identity presentation](https://www.slideshare.net/FabianVogelsteller/erc-725-identity)
- [In-contract claim VS claim registry](https://github.com/ethereum/wiki/wiki/ERC-735:-Claim-Holder-Registry-vs.-in-contract)
- [Identity related reports](https://www.weboftrust.info/specs.html)
- [W3C Verifiable Claims Use Cases](https://w3c.github.io/vc-use-cases/)
- [Decentralised Identity Foundation](https://identity.foundation)
- [Sovrin Foundation Self Sovereign Identity](https://sovrin.org/wp-content/uploads/2017/06/The-Inevitable-Rise-of-Self-Sovereign-Identity.pdf)

## Copyright

Copyright and related rights waived via [CC0](https://creativecommons.org/publicdomain/zero/1.0/).
